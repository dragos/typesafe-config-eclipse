grammar com.typesafe.config.Hocon

hidden (SL_COMMENT, ML_COMMENT, SPACE)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate hocon "http://www.typesafe.com/config/Hocon"

// here it would be good to find a way to
// create a synthetic HObject around the members...
Root: 
  NL*
  ( HObject
    | ( members += Member (',' | NL) NL* )*
  )
  NL*
;

HObject:
 '{' NL*
    ( members += Member (',' | NL) NL* )*
 '}';


Member:
  key = Path (':' NL* | '=' NL*)? value = Literal
;
  
Literal:
  (value += SimpleLiteral)+ | HObject | Array ;

SimpleLiteral returns EString:
  (StringLiteral | name = Boolean | Null | NumberLiteral)
;

Array:
  ('[' NL*
      (values+=Literal) 
      ((',' | NL) NL* values+=Literal)*
      NL* 
  ']') | ( '[' NL* ']' )
;

Boolean:
  'true' | 'false' | 'on' | 'off'
;

Null:
  {Null} 'null';

Path:
  elements += StringLiteral ( '.' elements += StringLiteral)*
;

StringLiteral returns EString: 
  (name = STRING | name = UNQUOTED_STRING)
;


NumberLiteral:
  value = Number
;

Number:
  '-'? INT ('.' INT)? (('E'|'e') '-'? INT)?;
  
terminal UNQUOTED_STRING:
  ( !(' ' | '\n' |'\t' | '\\' | '"' | '$' |
      '{' |  '}' | '[' | ']'  | ':' | '=' |
      '|' |  '+' | '#' | '`'  | '^' | '?' |
      '!' |  '@' | '*' | '&'  | '//'| ',' | '.'
  ) )+
;

terminal INT returns ecore::EInt: ('0'..'9')+;

terminal STRING : 
      '"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
      "'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
;

terminal ML_COMMENT : '/*' -> '*/';
// The single line comment won't include the EOL character at the end. That will be returned
// as a separate NL token. This way comments can be used as separators at the end of a meaningful 
// line, like # this is a comment that separates two members
terminal SL_COMMENT   : ('//'|'#') !('\n'|'\r')*;

terminal SPACE:
  (' ' | '\t')+
;

terminal NL:
  '\n'+
;